plugins {
  id 'java'
  id 'application'
  id 'org.sonarqube' version '2.1'
  id 'com.jfrog.bintray' version '1.7.3'
}

ext.moduleName = 'com.sedmelluq.udp-queue'
version = '1.0.6.1'

dependencies {
  compile 'com.sedmelluq:lavaplayer-common:1.0.5'
  compile "org.slf4j:slf4j-api:$slf4jVersion"
}

compileJava.dependsOn(':udp-queue-natives:checkNatives')

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId rootProject.group
      artifactId moduleName

      from components.java

      artifact sourceJar {
        classifier "sources"
      }
    }
  }

  repositories {
    maven {
      credentials {
        username = nexusUsername
        password = nexusPassword
      }
      if (project.version.endsWith('-SNAPSHOT')) {
        url "https://repo.npstr.space/repository/maven-snapshots/"
      } else {
        url "https://repo.npstr.space/repository/maven-releases/"
      }
    }
  }
}

apply from: "${rootDir}/gradle/bintray.gradle"
